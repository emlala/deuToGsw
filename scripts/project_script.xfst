! Emmi Lahtisalo
! Standard German - Swiss German translator

clear stack

read lexc project_lexicon.lexc
define Lexicon ;
regex Lexicon ;

! Note that this list might not be exhaustive:

define Vowel	[ a | e | i | o | u | ü | ä | ö | äi | ei | oi | äu | ou | iö | öi | uö| üö ] ;
define Cons	[ b | c | d | f | g | h | j | k | l | m | n | p | q | r | s | t | v | w | z ] ;


define TeToT		[ t e -> t || _ .#. ] ;


define ZehnToZää	[ e h n -> ä ä || [z] _ ] ; 
! 10

define IgToG		[ i g -> g || [ s s | z ] _ ] ;


define SsToS		[ s s -> s || [y] _ ] ; 
! dryssg -> drysg

define StToScht		[ 0 -> c h || [ e r | g ] [s] _ [t] ] ; 
! 1. and all ordinals >= 20

define 0ToI 		[ 0 -> i || [g] _ {scht} ] ; 
! all ordinals >= 20

define NTo0		[ n -> 0 || [ e | i | h | ü ] _ ]  ; 
! 1, 5, 7, 10 

define EiToÄi		[ {ei} -> {äi} ] ; 
! 1, 2,

define EToÄ		[ e -> ä || _ [ c h | u ] ] ; 
! 6, 9, this and the previous one could be combined but left separate to keep things clear

define fünf		[ {füf} -> {föif} || _ [ .#. | t | u ] ] ; 
! 5, 5.

define ÄToÜ		[ ä -> ü || [ r | n ] _  ] ; 
! 3, 9

define UIToÜ		[ [ i | u ] -> ü || [ü] _ ] ; 
! 3, 9

define ÜüToI		[ ü ü -> i || _ [z] ] ; 
! 13

define ÜüToY		[ ü ü -> y || _ [s] ] ; 
! 30

define sibet		[ 0 -> e || [b] _ [t] ] ; 
! 7.

define ETo0		[ e -> 0 || [i] _ [b] ] ; 
! 7

define UndToE		[ {und} -> e || [ n | i | r | f | s | ü | t | e ] _ ] ;


define ÄiToÄin		[ 0 -> n || .#. {äi} _ [e] ] ; 
! ein-und...

define sibe		[ [ 0 | ä ] -> e || [b] _ [ z g i | u ] ] ;
! 17., sieben-und...


read regex Lexicon .o.
TeToT .o. 
ZehnToZää .o. 
IgToG .o. 
SsToS .o. 
StToScht .o. 
0ToI .o. 
NTo0 .o. 
EiToÄi .o.
EToÄ .o.
fünf .o. 
ÄToÜ .o.
UIToÜ .o.
ÜüToI .o. 
ÜüToY .o. 
sibet .o.
ETo0 .o.
UndToE .o.
ÄiToÄin .o.
sibe ;
